task integrationTests() {
  description = "Run integration tests"
}

tasks.addRule("integrationTestFor-<SampleName>") { String taskName ->
  if (taskName.startsWith("integrationTestFor-")) {
    String sample = taskName - "integrationTestFor-"
    File projectDir = file("$rootDir/samples/$sample")
    tasks.create(taskName, GradleBuild) {
      dir = projectDir
      buildFile = new File(projectDir, "build.gradle")
      tasks = ['clean', 'check']
    }
  }
}

new File(projectDir, "samples").listFiles().each {
  if (it.directory) {
    tasks.integrationTests.dependsOn "integrationTestFor-$it.name"
  }
}

tasks.integrationTests.dependsOn ":command-line:verifyTest", ":command-line:verifySwiftGenTest"
